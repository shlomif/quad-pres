import os

env = Environment()

# TODO : Implement a user-specified perl path 

# What to do:
# 1. Read the option.
# 2. If None - keep PERL_PATH as is.
# 3. If empty - find one from the WhereIs.
# 4. If not empty - assign it as the Perl interpreter.
# 5. If the setting is not the same as PERL_PATH, or if the file PERL_PATH.txt
#    does not exist change the setting in the file PERL_PATH.txt.
# 6. Make sure all the scripts that require PERL_PATH depend upon 
#    the file.

if (ARGUMENTS.has_key('PATH_PERL') or not os.access('PATH_PERL.setting', os.F_OK)):
    path_perl = ARGUMENTS.get('PATH_PERL', '')
    if path_perl == '':
        path_perl = env.WhereIs("perl")
    if os.access('PATH_PERL.setting', os.F_OK):
        existing = open('PATH_PERL.setting').read()
    else:
        existing = None
    if existing != path_perl:
        f = open('PATH_PERL.setting', 'wb')
        f.write(path_perl)
        f.close()
       
config_file = 'quad-pres.conf'
opts = Options(config_file)
perl_path = env.WhereIs("perl");

opts.Add(PathOption('PREFIX',
    'Directory to install under (Default: /usr/local)',
    '/usr/local'))
opts.Add('DESTDIR', 
        'Prefix to append to the installation directory (without affecting the paths in the files) - Default: empty',
    '')
opts.Update(env)
opts.Save(config_file, env)

# Paths
prefix = env['PREFIX']
datadir = prefix + '/share'
pkgdatadir = datadir + '/quad-pres'
modulesdir = pkgdatadir + '/perl5'
qpscriptsdir = pkgdatadir + '/scripts'

def build_script_file(env, target, source):
    filename = str(target[0])
    f = open(filename+".pl", 'rt');
    lines = f.readlines()
    f.close()
    new_lines = []
    for l in lines:
        # TODO : Replace it with something better, based on PREFIX, etc.
        new_lines.append(l.replace('{QP_MODULES_DIR}', modulesdir))
    lines = new_lines
    f = open("PATH_PERL.setting")
    path_perl = f.read()
    f.close()
    lines[0] = lines[0].replace('/usr/bin/perl', path_perl)
    f = open(filename, 'wt')
    f.writelines(lines)
    f.close()
    os.chmod(filename, 0755)

perl5_scripts = ["clear-tree.pl", "fix-gvim-html.pl", "html-server-to-hd.pl",
    "Render_all_contents.pl", "render-file.pl", "upload.pl"]

perl5_scripts_dir = "perl5/scripts"

for script in perl5_scripts:
    s_path = perl5_scripts_dir + "/" + script
    env.Alias('build',
        env.Command(s_path,
            [s_path+".pl", "PATH_PERL.setting", "quad-pres.conf"],
            build_script_file
       )
    )
    env.Alias('install',
        env.Install(qpscriptsdir, env.File(script, perl5_scripts_dir))
    )

# Set the default target to build.
env.Default('build');

